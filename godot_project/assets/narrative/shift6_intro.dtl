# shift6_intro.dtl
audio music "res://assets/music/narrative/horror_abyss_main.mp3" [fade="2.0" volume="-5.0" bus="Music"]
audio music "res://assets/audio/ambient/horror_drone_12.mp3" [fade="2.0" volume="-5.0" bus="Ambient"]
[background arg="res://assets/narrative/occupation_checkpoint.png" fade="0.0"]
join narrator [z_index="1"]
narrator: The border facility is in complete lockdown, with armed security patrolling every corridor. #id:b8
leave narrator

[background arg="res://assets/narrative/security_lockdown.png" fade="1.2"]
join supervisor_russet [z_index="1"]
supervisor_russet: [pulse freq=3][color=RED]ATTENTION! LEVEL RED security situation![/color][/pulse] Classified documents were stolen last night. #id:b9
supervisor_russet: We have a [tornado radius=4 freq=3][color=CRIMSON]traitor in our midst[/color][/tornado]. Anyone withholding information will be treated as complicit. #id:ba
leave supervisor_russet

[background arg="res://assets/narrative/maximum_security.png" fade="2.0"]
join security_officer [z_index="1"]
security_officer: All officers, line up for [wave amp=15 freq=2][color=DARK_ORANGE]security scanning[/color][/wave]. Those with elevated stress responses will be flagged. #id:bb
leave security_officer

[background arg="res://assets/narrative/checkpoint_corridor.png" fade="2.0"]
join murphy [z_index="1"]
murphy: Have you seen what's happening outside? Pamphlets everywhere showing [color=DARK_VIOLET]Root Reserve[/color] is made from... [pulse freq=3][color=CRIMSON]from us[/color][/pulse]. #id:bc

set {fellow_officer_response} = "none"
- "Keep your voice down. We're all being watched." #id:bd
	set {fellow_officer_response} = "cautious"
- "I've heard the same rumors. It's horrifying if true." #id:be
	set {fellow_officer_response} = "sympathetic"
- "That's ridiculous propaganda. Don't spread such lies." #id:bf
	set {fellow_officer_response} = "loyal"

if {fellow_officer_response} == "cautious":
	join murphy [z_index="1"]
	murphy: You're right. But if it's true... what does that make us? #id:c0
	leave murphy
elif {fellow_officer_response} == "sympathetic":
	join murphy [z_index="1"]
	murphy: My cousin was rejected last month. Does that mean...? I [color=CHARTREUSE]can't do this anymore[/color]. #id:c1
	leave murphy
else:
	join murphy [z_index="1"]
	murphy: Their expression becomes guarded. Of course. Loyal Spud forever. #id:c2
	leave murphy

[background arg="res://assets/narrative/interrogation.png" fade="2.0"]
audio music "res://assets/music/narrative/horror_fog_main.mp3" [fade="1.5" volume="-5.0" bus="Music"]
join security_chief [z_index="1"]
security_chief: State your whereabouts last night between 2200 and 0400 hours. #id:c3

set {interrogation_response} = "none"
- "I was in my quarters the entire night, sleeping." #id:c4
	set {interrogation_response} = "lie"
- "I'd prefer to speak to my legal representative first." #id:c5
	set {interrogation_response} = "legal"

if {interrogation_response} == "lie":
	join security_chief [z_index="1"]
	security_chief: Your stress levels indicate [color=YELLOW]deception[/color]. However... inconclusive. You may go for now. #id:c6
	leave security_chief
else:
	join security_chief [z_index="1"]
	security_chief: Legal representative? This is a security procedure, not a trial. Your refusal has been noted. #id:c7
	leave security_chief

# Viktor encounter - if player discovered his wife on manifest
if {viktor_wife_discovery} == "yes":
	[background arg="res://assets/narrative/checkpoint_corridor.png" fade="1.5"]
	audio "res://assets/audio/footsteps/Metal Walk 1_2.mp3" [volume="-8.0" bus="SFX"]
	join viktor [z_index="1"]
	viktor: A burly security guard approaches during your break. His face is haggard. #id:c7_viktor1
	viktor: Hey... you're one of the customs officers, right? I've been asking around about my wife, Elena. #id:c7_viktor2
	viktor: She crossed this checkpoint three weeks ago. [color=STEEL_BLUE]Failed inspection.[/color] I haven't heard from her since. #id:c7_viktor3
	audio "res://assets/audio/stings/suspicion_sting.mp3" [volume="-8.0" bus="SFX"]

	set {viktor_conversation} = "none"
	- "Viktor... I found something. About Elena. You need to know the truth." #id:c7_viktor4
		set {viktor_conversation} = "tell_truth"
		set {viktor_allied} = "yes"
	- "I'm sorry, Viktor. I haven't seen anything." (Lie to protect him) #id:c7_viktor5
		set {viktor_conversation} = "lie_protect"

	if {viktor_conversation} == "tell_truth":
		audio "res://assets/audio/paper/paper handled 1.wav" [volume="-5.0" bus="SFX"]
		viktor: You show him a copy of the manifest page. His face goes white. #id:c7_viktor6
		viktor: [wave amp=25 freq=3]His hands tremble as he reads.[/wave] [color=GOLD]Elena Petrov[/color]... [color=CRIMSON]Processed... Batch RR-2851...[/color] #id:c7_viktor7
		audio "res://assets/audio/stings/dread_chord.mp3" [volume="-5.0" bus="SFX"]
		viktor: His voice is barely a whisper. [tornado radius=3 freq=2][color=CRIMSON]They... they MADE her into...[/color][/tornado] #id:c7_viktor8
		viktor: His grief transforms into cold fury. [pulse freq=2][color=CRIMSON]Idaho will pay for this.[/color][/pulse] I swear it on her memory. #id:c7_viktor9
		viktor: He grips your shoulder. [color=CHARTREUSE]Thank you for telling me.[/color] When the time comes... I'll remember this. #id:c7_viktor10
		leave viktor
	else:
		viktor: He nods sadly. [color=STEEL_BLUE]I thought so.[/color] No one knows anything. Or no one wants to talk. #id:c7_viktor11
		viktor: If you hear anything... [color=YELLOW]please.[/color] She's all I have. #id:c7_viktor12
		leave viktor

[background arg="res://assets/narrative/resistance_planning.png" fade="2.0"]
audio music "res://assets/music/narrative/electronic_reborn_main.mp3" [fade="2.0" volume="-8.0" bus="Music"]
join sweet_potato_sasha [z_index="1"]
sweet_potato_sasha: It's working. The manifests have caused an uproar, but [color=DARK_ORANGE]Russet suspects us[/color]. #id:c8
sweet_potato_sasha: Watch for allies from our organization during today's shift. [color=CHARTREUSE]You'll recognize them[/color]. #id:c9
sweet_potato_sasha: If you can get them through safely, use the [color=CHARTREUSE]special mark we discussed[/color]. #id:ca

set {sasha_plan_response} = "none"
- "It's worth the risk. I'm in." #id:cb
	set {sasha_plan_response} = "committed"
- "This is getting too dangerous. Maybe we should lay low." #id:cc
	set {sasha_plan_response} = "nervous"

if {sasha_plan_response} == "committed":
	join sweet_potato_sasha [z_index="1"]
	sweet_potato_sasha: I knew I could count on you. When this is over, [color=CHARTREUSE]Spud will know who the real heroes were[/color]. #id:cd
	leave sweet_potato_sasha
else:
	join sweet_potato_sasha [z_index="1"]
	sweet_potato_sasha: We're way past that point. Laying low won't save us. [color=CHARTREUSE]Only bringing down the system will[/color]. #id:ce
	leave sweet_potato_sasha

[background arg="res://assets/narrative/supervisor_presenting.png" fade="2.0"]
join supervisor_russet [z_index="1"]
supervisor_russet: Each station now has the new [color=DARK_ORANGE]RealityScan system[/color]. It detects unauthorized markings and forgeries. #id:cf

supervisor_russet: All documents must be scanned before approval. The [color=CRIMSON]traitors will be rooted out[/color]. I guarantee it. #id:d0
leave supervisor_russet

# RealityScan QTE Sequence - a tense moment during gameplay
[background arg="res://assets/narrative/checkpoint_booth.png" fade="1.5"]
audio music "res://assets/music/narrative/horror_hush_main.mp3" [fade="1.0" volume="-3.0" bus="Music"]
join narrator [z_index="1"]
narrator: Later that shift, a traveler approaches with papers bearing the [color=CHARTREUSE]subtle resistance mark[/color]. #id:d0_qte1
narrator: The [color=DARK_ORANGE]RealityScan[/color] begins its sweep. [wave amp=15 freq=3][color=RED]It's going to detect the mark![/color][/wave] #id:d0_qte2
leave narrator

join player_character [z_index="1"]
player_character: You have seconds to decide. The scanner is warming up... #id:d0_qte3
leave player_character

set {scanner_choice} = "none"
- "Fake a scanner malfunction - quickly!" (Help the resistance) #id:d0_qte4
	set {scanner_choice} = "help"
	set {helped_operative} = "yes"
- "Let the scanner complete its scan" (Follow protocol) #id:d0_qte5
	set {scanner_choice} = "scan"
	set {betrayed_resistance} = "yes"

if {scanner_choice} == "help":
	[background arg="res://assets/narrative/checkpoint_booth.png" fade="0.3"]
	audio "res://assets/audio/stings/heartbeat_tension.mp3" [volume="-5.0" bus="SFX"]
	join narrator [z_index="1"]
	narrator: [pulse freq=3][color=YELLOW]You need to work fast![/color][/pulse] Trigger the malfunction before it finishes scanning! #id:d0_qte6
	leave narrator
	[signal arg="qte_scanner_fake"]

	# QTE success continuation
	[background arg="res://assets/narrative/checkpoint_booth.png" fade="0.5"]
	audio "res://assets/audio/mechanical/feedback 2.wav" [volume="-5.0" bus="SFX"]
	join player_character [z_index="1"]
	player_character: You fumble with the scanner cables. Sparks fly. The screen flickers and dies. #id:d0_qte7
	leave player_character

	join supervisor_russet [z_index="1"]
	audio "res://assets/audio/footsteps/Concrete Walk 1_1.wav" [volume="-8.0" bus="SFX"]
	supervisor_russet: Russet rushes over. [color=YELLOW]What happened here?[/color] #id:d0_qte8

	set {malfunction_excuse} = "none"
	- "Power surge, sir. These new systems are temperamental." #id:d0_qte9
		set {malfunction_excuse} = "technical"
	- "I don't know, sir. It just died on me." #id:d0_qte10
		set {malfunction_excuse} = "innocent"

	if {malfunction_excuse} == "technical":
		supervisor_russet: Russet grunts. [color=STEEL_BLUE]Bureaucrats giving us untested equipment.[/color] Process them manually and flag for re-inspection. #id:d0_qte11
	else:
		supervisor_russet: Russet eyes you suspiciously. [color=YELLOW]How convenient.[/color] I'll have maintenance look at it. Don't let this happen again. #id:d0_qte12

	leave supervisor_russet
	join traveler_1 [z_index="1"]
	traveler_1: The resistance member gives you the slightest nod of gratitude as they pass through. #id:d0_qte13
	leave traveler_1
else:
	[background arg="res://assets/narrative/maximum_security.png" fade="0.5"]
	audio "res://assets/audio/stings/alarm_distant.mp3" [volume="-5.0" bus="SFX"]
	join narrator [z_index="1"]
	narrator: The scanner [pulse freq=3][color=RED]BEEPS LOUDLY[/color][/pulse]. Unauthorized markings detected! #id:d0_qte14
	leave narrator

	join supervisor_russet [z_index="1"]
	supervisor_russet: Russet appears instantly. [color=CHARTREUSE]Well done![/color] Another traitor caught. #id:d0_qte15
	join traveler_1 [z_index="1"]
	traveler_1: The resistance member is dragged away, terror in their eyes. "[color=CRIMSON]Please, no! I have children![/color]" #id:d0_qte16
	leave traveler_1
	supervisor_russet: Your loyalty is [color=DARK_ORANGE]noted in your file[/color]. Keep this up. #id:d0_qte17
	leave supervisor_russet

join player_character [z_index="1"]
player_character: Helping resistance members just got much more dangerous, but [color=CHARTREUSE]the truth must spread[/color]. #id:d1
leave player_character