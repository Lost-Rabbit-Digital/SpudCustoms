# shift5_intro.dtl
audio music "res://assets/audio/ambient/horror_drone_49.mp3" [fade="2.0" volume="-5.0" bus="Ambient"]
audio music "res://assets/music/narrative/ambient_evil_suspense_main.mp3" [fade="2.0" volume="-5.0" bus="Music"]
[background arg="res://assets/narrative/checkpoint_corridor.png" fade="0.0"]
join narrator [z_index="1"]
narrator: The border facility feels different today. Tense. Security patrols in greater numbers as wary silence replaces chatter. #id:a6
leave narrator

[background arg="res://assets/narrative/supervisor_presenting.png" fade="1.2"]
join supervisor_russet [z_index="1"]
supervisor_russet: Last night, [pulse freq=2][color=CRIMSON]prohibited materials[/color][/pulse] were discovered. Pamphlets containing lies about [color=DARK_VIOLET]Root Reserve[/color]. #id:a7
supervisor_russet: We're implementing [wave amp=15 freq=2][color=DARK_ORANGE]threat level ORANGE protocols[/color][/wave]. All officers subject to loyalty screenings. All travelers face maximum scrutiny. #id:a8
leave supervisor_russet

[background arg="res://assets/narrative/resistance_base.png" fade="2.0"]
join sweet_potato_sasha [z_index="1"]
sweet_potato_sasha: It's working. The pamphlets are spreading. People are asking questions about [color=DARK_VIOLET]Root Reserve[/color]. #id:a9
sweet_potato_sasha: I have sources telling me there's a shipment log in Russet's office - transport manifests that could [tornado radius=3 freq=2][color=CHARTREUSE]prove everything[/color][/tornado]. #id:aa
sweet_potato_sasha: I need you to help me get it. Tonight, after final shift. Can I count on you? #id:ab

set {sasha_investigation} = "none"
- "I'm in. Let's expose the truth." #id:ac
	set {sasha_investigation} = "committed"
- "I don't know, Sasha...this could be dangerous." #id:ad
	set {sasha_investigation} = "hesitant"

if {sasha_investigation} == "committed":
	join sweet_potato_sasha [z_index="1"]
	sweet_potato_sasha: Thank goodness. I knew you had [color=CHARTREUSE]stronger roots than the others[/color]. We'll meet at midnight. #id:ae
	leave sweet_potato_sasha
else:
	join sweet_potato_sasha [z_index="1"]
	sweet_potato_sasha: Sometimes we have to [color=CHARTREUSE]risk getting mashed to do what's right[/color]. Think about it. #id:af
	leave sweet_potato_sasha

[background arg="res://assets/narrative/maximum_security.png" fade="2.0"]
audio music "res://assets/music/narrative/horror_downward_main.mp3" [fade="2.0" volume="-5.0" bus="Music"]
join security_officer [z_index="1"]
security_officer: [color=DARK_ORANGE]Loyalty check[/color]. Please state your name and officer ID. Then tell me why you serve Spud. #id:b0

set {loyalty_response} = "none"
- "I serve to protect our borders from dangerous influences." #id:b1
	set {loyalty_response} = "patriotic"
- "I serve to ensure everyone receives fair treatment." #id:b2
	set {loyalty_response} = "idealistic"

if {loyalty_response} == "patriotic":
	join security_officer [z_index="1"]
	security_officer: Good answer. Spud needs loyal protectors. Scanner reads green. Carry on, officer. #id:b3
	leave security_officer
else:
	join security_officer [z_index="1"]
	security_officer: The officer's eyebrows raise. Fair treatment? Scanner flashes [color=DARK_ORANGE]amber[/color]. Remember, our primary concern is security, not fairness. #id:b4
	leave security_officer

# Viktor introduction - security guard worried about his missing wife
[background arg="res://assets/narrative/checkpoint_corridor.png" fade="1.5"]
audio "res://assets/audio/footsteps/Metal Walk 1_2.mp3" [volume="-8.0" bus="SFX"]
join viktor [z_index="1"]
viktor: A burly security guard catches your arm as you pass. His face is lined with worry. #id:b4_viktor1
viktor: Hey... sorry to bother you. You're one of the customs officers, right? Name's [color=CHARTREUSE]Viktor[/color]. #id:b4_viktor2
viktor: I've been asking everyone about my wife, [color=GOLD]Elena[/color]. Elena Petrov. She crossed this checkpoint three weeks ago. #id:b4_viktor3
viktor: [color=STEEL_BLUE]Failed inspection.[/color] They said she'd be processed and returned home, but... I haven't heard anything. #id:b4_viktor4
audio "res://assets/audio/stings/suspicion_sting.mp3" [volume="-10.0" bus="SFX"]
viktor: His voice wavers. No one will tell me anything. Do you know where the rejected travelers go? #id:b4_viktor5

set {viktor_initial_response} = "none"
- "I'm sorry, Viktor. I don't know. But I'll keep an eye out." #id:b4_viktor6
	set {viktor_initial_response} = "sympathetic"
- "I can't discuss processing details. Security protocols." #id:b4_viktor7
	set {viktor_initial_response} = "distant"

if {viktor_initial_response} == "sympathetic":
	viktor: Viktor's eyes show a flicker of hope. [color=CHARTREUSE]Thank you.[/color] Anything you find out... please. She's all I have. #id:b4_viktor8
else:
	viktor: Viktor nods, dejected. [color=STEEL_BLUE]Right. Protocols.[/color] I understand. #id:b4_viktor9
	viktor: But if you change your mind... [color=YELLOW]I need to know what happened to her.[/color] #id:b4_viktor10

leave viktor

[background arg="res://assets/narrative/checkpoint_booth.png" fade="2.0"]
audio music "res://assets/music/narrative/ambient_evil_suspense_main.mp3" [fade="2.0" volume="-5.0" bus="Music"]
join supervisor_russet [z_index="1"]
supervisor_russet: Today's quota has been increased. Anyone with suspicious materials is to be flagged for [color=DARK_VIOLET]special processing[/color]. #id:b5
supervisor_russet: I'll be [color=DARK_ORANGE]watching your decisions[/color] very carefully today. #id:b6
leave supervisor_russet

join player_character [z_index="1"]
player_character: Tonight could change everything. But first, [color=YELLOW]survive the day without raising suspicion[/color]. #id:b7
leave player_character