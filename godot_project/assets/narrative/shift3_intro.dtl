# shift3_intro.dtl
audio music "res://assets/music/narrative/horror_hallways_main.mp3" [fade="2.0" volume="-5.0" bus="Music"]
[background arg="res://assets/narrative/checkpoint_exterior.png" fade="0.0"]
audio "res://assets/audio/ambient/1_birds_outside.wav" [volume="-5.0" bus="Ambient"]
join narrator [z_index="1"]
narrator: Another day at the checkpoint. The line of potatoes stretches longer than ever, with everyone on edge. #id:78
leave narrator

[background arg="res://assets/narrative/customs_office.png" fade="1.5"]
audio "res://assets/audio/furniture/Door Open C.wav" [volume="-8.0" bus="SFX"]
join supervisor_russet [z_index="1"]
supervisor_russet: New orders from the top. We're [wave amp=15 freq=3][color=DARK_ORANGE]doubling rejections[/color][/wave]. Any potato deemed unsafe is detained immediately. #id:79
audio "res://assets/audio/stings/threat_reveal.mp3" [volume="-5.0" bus="SFX"]
leave supervisor_russet

[background arg="res://assets/narrative/checkpoint_interior.png" fade="2.0"]
audio "res://assets/audio/footsteps/Metal Walk 1_3.wav" [volume="-5.0" bus="SFX"]
join supervisor_russet [z_index="1"]
supervisor_russet: I have a special assignment. We've got intel on [pulse freq=2][color=CRIMSON]troublemakers[/color][/pulse]. Be vigilant with your inspections. #id:7a

set {scanner_response} = "none"
- "Yes sir, I won't let you down" (Loyal) #id:7b
	set {scanner_response} = "loyal"
- "What kind of troublemakers are we talking about?" (Questioning) #id:7c
	set {scanner_response} = "questioning"

if {scanner_response} == "loyal":
	join supervisor_russet [z_index="1"]
	supervisor_russet: Good. I knew I could count on you. You'll go far with that attitude. #id:7d
	leave supervisor_russet
else:
	join supervisor_russet [z_index="1"]
	supervisor_russet: The kind that [color=CRIMSON]threaten our way of life[/color]. Don't ask too many questions if you know what's good for you. #id:7e
	leave supervisor_russet

[background arg="res://assets/narrative/checkpoint_booth.png" fade="1.2"]
audio music "res://assets/music/ambient_vol3_sad_decisions_main.mp3" [fade="1.5" volume="-5.0" bus="Music"]
audio "res://assets/audio/stings/dread_chord.mp3" [volume="-5.0" bus="SFX"]
join npc_3 [z_index="1"]
npc_3: [color=STEEL_BLUE]Please, I beg you![/color] My wife - her name is [color=GOLD]Maris[/color] - she was supposed to be right behind me. #id:7f
npc_3: She [tornado radius=2 freq=3]never made it through customs[/tornado]. They said her papers had an "irregularity." #id:7fb
npc_3: No one will tell me what happened to her. Please, [color=CHARTREUSE]help me find her![/color] #id:80

set {family_response} = "none"
- "I'm sorry, but there's nothing I can do." (Refuse) #id:81
	set {family_response} = "refuse"
- "Let me see what I can find out. No promises." (Help) #id:82
	set {family_response} = "help"

if {family_response} == "refuse":
	npc_3: Just like all the others... [color=STEEL_BLUE]May your conscience weigh heavy as mine does.[/color] #id:83
	npc_3: The desperate potato shuffles away. You notice her staring at a worn photo before she disappears. #id:83b
	leave npc_3
	set {has_wife_photo} = "no"
else:
	audio "res://assets/audio/paper/paper picked up 2.wav" [volume="-5.0" bus="SFX"]
	npc_3: Thank you! Even a small hope is better than none. #id:84
	npc_3: She presses a [color=GOLD]worn photograph[/color] into your hands. Her name is [color=GOLD]Maris Piper[/color]. She has kind eyes. Please find her. #id:84b
	leave npc_3
	set {has_wife_photo} = "yes"
	set {wife_name} = "Maris Piper"

# Player examines the photo if they took it
if {has_wife_photo} == "yes":
	[background arg="res://assets/narrative/wife_photo.png" fade="1.0"]
	join player_character [z_index="1"]
	player_character: I look at the photo. A young potato with gentle features and a warm smile. [color=GOLD]Maris Piper[/color]. #id:84c
	player_character: Where did she go? The rejection queue leads to... where exactly? #id:84d
	leave player_character

[background arg="res://assets/narrative/checkpoint_corridor.png" fade="1.2"]
audio music "res://assets/music/narrative/horror_fog_main.mp3" [fade="2.0" volume="-5.0" bus="Music"]
audio "res://assets/audio/stings/suspicion_sting.mp3" [volume="-5.0" bus="SFX"]
join supervisor_russet [z_index="1"]
supervisor_russet: I saw that exchange. [color=DARK_ORANGE]Sympathizing with rejects only leads to trouble.[/color] Stay focused on your duty. #id:85
leave supervisor_russet