### Start intro_sequence.dtl
[background arg="res://assets/narrative/nation_of_spud_1.png" fade="0.0"]
join narrator [z_index="1"]
narrator: Welcome to the great nation of Spud, citizen.
narrator: A land of prosperity and opportunity for all potatoes.
narrator: But our beloved Spud faces challenges, and we need your help.
narrator: You've been selected for a crucial role in protecting our borders.
leave narrator

[background arg="res://assets/narrative/checkpoint_interior.png" fade="2.0"]
join narrator [z_index="1"]
narrator: Your duty\: to process incoming potatoes and maintain our way of life.
narrator: Don't forget, your family's well-being depends on your performance.
narrator: After all, you wouldn't want them to end up like...
narrator: [i]clears throat[/i] Nevermind. Let's move on.
leave narrator

[background arg="res://assets/narrative/great_wall_of_spud.png" fade="2.0"]
join narrator [z_index="1"]
narrator: BEHOLD! The mighty wall that safeguards our nation.
narrator: This is where you'll serve Spud, ensuring only the worthy enter.
narrator: Your vigilance is our first line of defense against undesirable elements.

join npc_1 [z_index="1"]
npc_1: [i]whispers[/i] <Have you noticed the food shortages lately?/The rations keep getting smaller, don't they?/Something's not right with the food supply.> Strange, with all the new Root Reserve shipments...
leave npc_1
narrator: Like they say, a good spud is a smooth spud. Any lumps could be...problematic.
leave narrator

[background arg="res://assets/narrative/supervisor_presenting.png" fade="2.0"]
join supervisor_russet [z_index="1"]
supervisor_russet: Ah, the new sprout has finally arrived. Welcome aboard.
supervisor_russet: Listen carefully, your role is vital to Spud's security.
supervisor_russet: By the authority vested in me, I appoint you as a Customs Officer of glorious Spud.
supervisor_russet: [i]firmly places official cap on your head[/i]
supervisor_russet: Uphold our laws, protect our borders. Spud's future depends on you.
supervisor_russet: Any questions before you begin?
set {initial_response} = "none"
- "I'm ready to serve Spud with all my starch!"
	set {initial_response} = "eager"
- "What exactly happens to rejected potatoes?"
	set {initial_response} = "questioning"
- "Is there a reason everyone seems so...nervous?"
	set {initial_response} = "observant"

if {initial_response} == "eager":
	supervisor_russet: [i]broad smile[/i] Excellent! That's the kind of enthusiasm we need.
	supervisor_russet: With dedication like that, you might even earn yourself a Root Reserve commendation.
	supervisor_russet: [i]claps your back heartily[/i] Keep that attitude, sprout. You'll do just fine.
	leave supervisor_russet

if {initial_response} == "questioning":
	supervisor_russet: [i]smile falters slightly[/i] They're... processed. According to protocol.
	supervisor_russet: [i]straightens up[/i] Root Reserve handles all the details. No need to concern yourself.
	supervisor_russet: [i]voice hardening[/i] Focus on your duties. Nothing more.
	leave supervisor_russet

else:
	supervisor_russet: [i]eyes narrow dangerously[/i] Nervous? Nonsense.
	supervisor_russet: The only ones who need be nervous are those who threaten Spud's security.
	supervisor_russet: [i]adjusts your cap uncomfortably tight[/i] I trust that won't be you, will it?
	leave supervisor_russet

[background arg="res://assets/narrative/intro_sequence_gameplay_shot.png" fade="2.0"]
You continue your shift...
